model {
  for (i in 1:n.obs) {
    gi[i] ~ dnorm(mu.year.indiv[year[i],indiv[i]], tau.resid)
  }

  ## expectation for individual j in year i is sum of year
  ## and indivdidual effects
  ##
  for (i in 1:n.years) {
    for (j in 1:n.indiv) {
      mu.year.indiv[i,j] <- mu.year[i] + mu.indiv[j]
    }
  }

  ## individuals centered around population mean, i.e.,
  ## individuals nested within sites
  ##
  for (i in 1:n.indiv) {
    mu.indiv[i] ~ dnorm(mu.site[site[i]], tau.indiv)
  }

  ## site means
  ##
  for (i in 1:n.sites) {
    mu.site[i] ~ dnorm(beta.0, tau.site)
  }

  ## n.months is number of months of prior weather included in
  ## calculating expectation
  ##
  for (i in 1:n.years) {
    mu.year[i] <- inprod(beta.ppt[1:n.months], ppt[i,1:n.months]) +
                  inprod(beta.tmn[1:n.months], tmn[i,1:n.months])
  }

  ## priors
  ##
  beta.0 ~ dnorm(0.0, tau.beta)
  for (i in 1:n.months) {
    beta.ppt[i] ~ dnorm(0.0, tau.beta)
    beta.tmn[i] ~ dnorm(0.0, tau.beta)
  }
  tau.resid ~ dgamma(1.0, 1.0)
  tau.indiv ~ dgamma(1.0, 1.0)
  tau.site ~ dgamma(1.0, 1.0)
  var.resid <- 1.0/tau.resid
  var.indiv <- 1.0/tau.indiv
  var.site <- 1.0/tau.site

  ## site indices
  ##
  for (i in 1:n.sites) {
    idx.site[i] <- inprod(beta.ppt[1:n.months], ppt.mean[1:n.months]) +
                   inprod(beta.tmn[1:n.months], tmn.mean[1:n.months]) +
                   mu.site[i];
  }
}